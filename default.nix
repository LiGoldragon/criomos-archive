{
  inputs ? (import ./mkInputs.nix),
}:
let
  inherit (inputs) nixpkgs;

  # TODO: re-design - Broken upstream
  lib = inputs.lib // (import ./libExtension.nix);

  mkCriomOS = import ./nix/mkCriomOS;

  localSources =
    let
      importInput = name: value: import value;
      modulePaths = {
        mkPkgs = ./nix/mkPkgs;
        mkWorld = ./nix/mkWorld;
        mkCrioSphere = ./nix/mkCrioSphere;
        mkCrioZones = ./nix/mkCrioZones;
        pkdjz = ./nix/pkdjz;
        homeModule = ./nix/homeModule;
        nodeNames = ./nodeNames.nix;
        files = ./nix/files; # TODO: use?
      };
    in
    mapAttrs importInput modulePaths;

  hob =
    let
      hobInputs = removeAttrs inputs nodeNames;
      adHocHobSpokes = {
        inherit (localSources) mkWebpage;
        pkdjz.HobWorlds = localSources.pkdjz;
      };
    in
    hobInputs // adHocHobSpokes;

  inherit (localSources)
    nodeNames
    mkPkgs
    homeModule
    mkWorld
    ;

  mkPkgsAndWorldFromSystem =
    system:
    let
      pkgs = mkPkgs { inherit nixpkgs lib system; };
    in
    {
      inherit pkgs;
      world = mkWorld {
        inherit
          lib
          pkgs
          system
          hob
          ;
      };
    };

  inherit (builtins) mapAttrs;

  generateCrioSphereProposalFromName =
    name:
    let
      subCriomeConfig = inputs."${name}".NodeProposal or { };
      explicitNodes = subCriomeConfig.nodes or { };
      implicitNodes = import ./implicitNodes.nix;
      allNodes = explicitNodes // implicitNodes;
    in
    subCriomeConfig // { nodes = allNodes; };

  uncheckedCrioSphereProposal = lib.genAttrs nodeNames generateCrioSphereProposalFromName;

  mkNodeDerivations =
    preNodeName: crioZone:
    let
      inherit (crioZone) users;
      inherit (crioZone.node) system;
      pkgsAndWorld = mkPkgsAndWorldFromSystem system;
      inherit (pkgsAndWorld) pkgs world;
      horizon = crioZone;

      userProfiles = {
        light = {
          dark = false;
        };
        dark = {
          dark = true;
        };
      };

      mkUserHomes =
        userName: user:
        let
          inherit (world) pkdjz;

          mkProfileHome =
            profileName: profile:
            let
              modules = [ homeModule ];
              extraSpecialArgs = {
                inherit
                  pkdjz
                  world
                  horizon
                  user
                  profile
                  ;
              };
              evalHomeManager = hob.home-manager.lib.homeManagerConfiguration;
              evaluation = evalHomeManager { inherit modules extraSpecialArgs pkgs; };
            in
            evaluation.config.home.activationPackage;
        in
        mapAttrs mkProfileHome userProfiles;

      mkUserEmacs =
        userName: user:
        let
          inherit (world.pkdjz) mkEmacs;
          mkProfileEmacs = profileName: profile: mkEmacs { inherit user profile; };
        in
        mapAttrs mkProfileEmacs userProfiles;

      commonArgs = {
        inherit
          lib
          world
          horizon
          homeModule
          hob
          ;
      };

    in
    {
      os = mkCriomOS ({ _withUsers = false; } // commonArgs);
      fullOs = mkCriomOS ({ _withUsers = true; } // commonArgs);
      hom = mapAttrs mkUserHomes users;
      emacs = mapAttrs mkUserEmacs users;
    };

  mkEachCrioZoneDerivations =
    crioZones:
    let
      mkNodeDerivationIndex = nodeName: nodePreNodeIndeks: mapAttrs mkNodeDerivations nodePreNodeIndeks;
    in
    mapAttrs mkNodeDerivationIndex crioZones;

  mkNixApiOutputsPerSystem =
    system:
    let
      pkgsAndWorld = mkPkgsAndWorldFromSystem system;
      inherit (pkgsAndWorld) pkgs world;
      inherit (pkgs) symlinkJoin linkFarm;

      mkHobOutput =
        name: src:
        symlinkJoin {
          inherit name;
          paths = [ src.outPath ];
        };

      hobOutputs = mapAttrs mkHobOutput hob;

      mkSpokFarmEntry = name: spok: {
        inherit name;
        path = spok.outPath;
      };

      allMeinHobOutputs = linkFarm "hob" (lib.mapAttrsToList mkSpokFarmEntry hobOutputs);

    in
    {
      packages = world // {
        inherit pkgs;
        hob = hobOutputs;
        fullHob = allMeinHobOutputs;
      };
    };

  # TODO: Consider using a 'system' input
  perSystemAllOutputs = inputs.flake-utils.lib.eachDefaultSystem mkNixApiOutputsPerSystem;

  proposedCrioSphere = localSources.mkCrioSphere { inherit uncheckedCrioSphereProposal lib; };
  proposedCrioZones = localSources.mkCrioZones { inherit lib proposedCrioSphere; };

in
perSystemAllOutputs
// {
  crioZones = mkEachCrioZoneDerivations proposedCrioZones;
}
