{
  lib,
  pkgs,
  criomos-lib,
  pkdjz,
  user,
  horizon,
  config,
  profile,
  world,
  litellmProxy,
  # Todo(data)
  ...
}:
let
  inherit (builtins) toString readFile toJSON;
  inherit (lib)
    optionalAttrs
    optionalString
    optionals
    mkIf
    optional
    ;
  inherit (pkdjz) kynvyrt;
  inherit (horizon) node;
  inherit (user.methods)
    useColemak
    hasPreCriome
    gitSigningKey
    matrixID
    sizedAtLeast
    isCodeDev
    isMultimediaDev
    ;
  inherit (user) githubId name methods;
  inherit (profile) dark;
  inherit (pkgs) writeText;

  homeDir = config.home.homeDirectory;

  isPrometheusNode = node.name == "prometheus";
  isOuranosNode = node.name == "ouranos";

  # Prefer a directly-routable IP for cross-node calls (Ouranos → Prometheus).
  # The criome domain name is not guaranteed to resolve in the current environment.
  prometheusCriomeHost =
    let
      prometheusNode = horizon.exNodes.prometheus or null;
      prometheusNodeIp =
        if prometheusNode != null && builtins.hasAttr "nodeIp" prometheusNode
        then prometheusNode.nodeIp
        else null;
      prometheusDomainName =
        if prometheusNode != null
        then prometheusNode.criomeDomainName
        else "prometheus.${horizon.cluster.name}.criome";
    in
    if prometheusNodeIp != null && prometheusNodeIp != ""
    then prometheusNodeIp
    else prometheusDomainName;

  # Current session runtime truth: the working overlay path is Prometheus tailnet IP.
  # System MagicDNS integration is not yet authoritative for user-space consumers.
  prometheusOverlayHost = if isOuranosNode then "100.64.0.1" else prometheusCriomeHost;

  terminalFontFamily = if sizedAtLeast.med then "FiraMono Nerd Font" else "DejaVu Sans Mono";

  # Todo(Those data files should be in a top arg called data)
  colemakZedKeys = criomos-lib.importJSON ./../../../data/ZedKeymaps/goldragon-colemak.json;

  fzfColemakBinds = import ./fzfColemak.nix;

  fzfBinds = (optionals useColemak fzfColemakBinds);

  mkFzfBinds = list: "--bind=" + (builtins.concatStringsSep "," list);

  fzfBindsString = optionalString (fzfBinds != [ ]) (mkFzfBinds fzfBinds);

  fzfTheme = if dark then import ./fzfDark.nix else import ./fzfLight.nix;
  fzfBase16Map = import ./fzfBase16map.nix;

  mkFzfColor =
    n: v:
    let
      color = fzfTheme.${v};
    in
    color;

  fzfColors = builtins.mapAttrs mkFzfColor fzfBase16Map;

  waylandQtpass = pkgs.qtpass.override { pass = waylandPass; };
  waylandPass = pkgs.pass.override {
    x11Support = false;
    waylandSupport = true;
  };

  fontPackages = with pkgs; [
    dejavu_fonts
    nerd-fonts.fira-mono
    nerd-fonts.fira-code
  ];

  mkFcCache = pkgs.makeFontsCache { fontDirectories = fontPackages; };

  mkFontPaths = lib.concatMapStringsSep "\n" (path: "  <dir>${path}/share/fonts</dir>") fontPackages;

  mkFontConf = ''
    <?xml version='1.0'?>
    <!DOCTYPE fontconfig SYSTEM 'fonts.dtd'>
    <fontconfig>
    ${mkFontPaths}
      <cachedir>${mkFcCache}</cachedir>
    </fontconfig>
  '';

  mkFootSrcTheme =
    themeName:
    let
      themeString = readFile (pkgs.foot.src + "/themes/${themeName}");
    in
    writeText "foot-theme-${themeName}" themeString;

  footThemeFile =
    let
      darkTheme = mkFootSrcTheme "derp";
      lightTheme = mkFootSrcTheme "selenized-white";
    in
    if dark then darkTheme else lightTheme;

  bleedingEdgeGraphicalPackages = [ ];

  modernGraphicalPackages = with pkgs; [
    # C
    mpv
    # ctags
    swaylock
    grim
    slurp
    wayland-warpd
    zathura
    wl-clipboard
    libnotify
    imv
    wf-recorder
    libva-utils
    ffmpeg-full
    # start("GTK")
    appflowy
    wofi
    gitg
    pwvucontrol # Pipewire audio GTK UI
    sonata
    dino
    # ptask # Broken
    transmission-remote-gtk
    # start("Qt")
    adwaita-qt
    qgnomeplatform
    waylandQtpass
    waylandPass
    # helvum # Broken? Pipewire nodes UI
    coppwr # Pipewire Nodes UI

    # TODO('horizon language')
    (pkgs.hunspell.withDicts (dicts: [
      dicts.en_GB-ize
      dicts.en_US
    ]))
    (aspellWithDicts (
      ds: with ds; [
        en
        en-computers
        en-science
      ]
    ))

  ];

  brootConfig = toJSON { };

  wayland-warpd = pkgs.warpd.override { withX = false; };

  unixUtilities =
    with pkgs;
    [
      dua # Disk usage
      lsof # List open files
      delta # Git diff viewew
      cpulimit # Limit a process' CPU usage
      yggdrasil
      usbutils
      pciutils
      efivar # Hardware
      lshw
      gptfdisk
      parted # Disk utils
      wireguard-tools
    ]
    ++ (optionals (node.machine.arch == "x86-64") [ i7z ]);

  programmingTools = with pkgs; [
    # C
    stdenv.cc
    # Rust
    cargo
    # pkdjz.nightlyRustDevEnv # TODO: debug - breaks zsh completions
    # Nix
    nil
    nixfmt-rfc-style
    npins
    # Clojure
    clojure
    babashka
    neil
    clj-kondo
    leiningen
    cljfmt
    # lisp
    zprint
    # Python
    python3
    ruff
    # Flashing
    avrdude
    # Shell
    shfmt
    # Other
    meld # GTK diff editor
    gg-jj # Jujutsu GUI
    lazyjj # # jujutsu TUI
    just
    difftastic
    tokei # Lines of code
  ];

  unixDeveloperPackages = unixUtilities ++ programmingTools;

  prometheusLlamaPort = 11436;
  prometheusLlamaApiKey = "sk-no-key-required";

  # Runtime model assets live in the user's home. Do not hard-code presets to
  # non-existent files; generate the preset at service start from the canonical
  # filenames that actually exist on disk.
  prometheusLlamaModelDir = "${homeDir}/.local/share/prometheus-llama/models";
  prometheusLlamaPreset = "${homeDir}/.config/prometheus-llama/models.ini";

  prometheusLlamaCanonicalModels = [
    {
      section = "prometheus-main-deepseek";
      file = "DeepSeek-R1-Distill-Llama-70B-Q8_0-merged.gguf";
      alias = "prometheus-deepseek-r1-distill-llama-70b";
    }
    {
      section = "prometheus-subagent-qwen25";
      file = "Qwen-2.5-72B-Instruct.gguf";
      alias = "prometheus-qwen-2.5-72b-instruct";
    }
    {
      section = "prometheus-fast-llama33";
      file = "Llama-3.3-70B-Instruct.gguf";
      alias = "prometheus-llama-3.3-70b-instruct";
    }
  ];

  prometheusLlamaGeneratePreset =
    pkgs.writeShellScript "prometheus-llama-generate-models-ini" (
      let
        mkAdd = m: "add_model ${lib.escapeShellArg m.section} ${lib.escapeShellArg m.file} ${lib.escapeShellArg m.alias}";
        addLines = builtins.concatStringsSep "\n" (builtins.map mkAdd prometheusLlamaCanonicalModels);
      in
      ''
        set -euo pipefail

        preset=${lib.escapeShellArg prometheusLlamaPreset}
        models_dir=${lib.escapeShellArg prometheusLlamaModelDir}
        coreutils=${lib.escapeShellArg pkgs.coreutils}/bin

        "$coreutils/mkdir" -p "$("$coreutils/dirname" "$preset")"

        {
          echo "version = 1"
          echo ""
          echo "[*]"
          echo "models-dir = $models_dir"
          echo "load-on-startup = false"
          echo ""
        } > "$preset"

        wrote_any=0

        add_model() {
          local section="$1"
          local filename="$2"
          local alias="$3"
          local path="$models_dir/$filename"

          if [[ -f "$path" ]]; then
            {
              echo "[$section]"
              echo "model = $path"
              echo "alias = $alias"
              echo ""
            } >> "$preset"
            wrote_any=1
          fi
        }

        ${addLines}

        if [[ "$wrote_any" -eq 0 ]]; then
          echo "# No canonical GGUF assets found under $models_dir; add at least one *.gguf." >> "$preset"
        fi
      ''
    );

  litellmRouterYaml = ''
    ---
    model_list:
      - model_name: prometheus-deepseek-r1-distill-llama-70b
        litellm_params:
          model: openai/prometheus-main-deepseek
          api_base: http://${prometheusOverlayHost}:${toString prometheusLlamaPort}/v1
          api_key: ${prometheusLlamaApiKey}
        order: 1
      - model_name: prometheus-qwen-2.5-72b-instruct
        litellm_params:
          model: openai/prometheus-subagent-qwen25
          api_base: http://${prometheusOverlayHost}:${toString prometheusLlamaPort}/v1
          api_key: ${prometheusLlamaApiKey}
        order: 2
      - model_name: prometheus-llama-3.3-70b-instruct
        litellm_params:
          model: openai/prometheus-fast-llama33
          api_base: http://${prometheusOverlayHost}:${toString prometheusLlamaPort}/v1
          api_key: ${prometheusLlamaApiKey}
        order: 3
      - model_name: cloud-reasoning
        litellm_params:
          model: openai/gpt-4o
          api_base: https://api.openai.com/v1
          api_key: os.environ/OPENAI_API_KEY
        order: 10
      - model_name: cloud-coder
        litellm_params:
          model: openai/gpt-4o-mini
          api_base: https://api.openai.com/v1
          api_key: os.environ/OPENAI_API_KEY
        order: 11
      - model_name: cloud-fast
        litellm_params:
          model: openai/gpt-4o-mini
          api_base: https://api.openai.com/v1
          api_key: os.environ/OPENAI_API_KEY
        order: 12
    router_settings:
      enable_pre_call_checks: true
      model_group_alias:
        main-deepseek: prometheus-deepseek-r1-distill-llama-70b
        subagent-qwen25: prometheus-qwen-2.5-72b-instruct
        fast-llama33: prometheus-llama-3.3-70b-instruct
      fallbacks:
        - main-deepseek:
            - cloud-reasoning
            - cloud-coder
        - subagent-qwen25:
            - cloud-coder
            - cloud-fast
        - fast-llama33:
            - cloud-fast
    litellm_settings:
      default_fallbacks:
        - fast-llama33
        - cloud-fast
      drop_params: true
      modify_params: true
      logging:
        level: info
  '';

  prometheusModelCatalogPath = ../../../data/config/pi/prometheus-model-catalog.json;
  prometheusModelCatalog = builtins.fromJSON (builtins.readFile prometheusModelCatalogPath);
  piAgentGatewayProvider =
    if builtins.hasAttr "provider" prometheusModelCatalog
    then prometheusModelCatalog.provider
    else "prometheus";
  piAgentGatewayApiKey = "sk-no-key-required";
  prometheusAliasPrefix = "${piAgentGatewayProvider}/";
  prometheusAliasPrefixLen = builtins.stringLength prometheusAliasPrefix;
  stripProviderPrefix = alias:
    let
      aliasLen = builtins.stringLength alias;
    in if aliasLen >= prometheusAliasPrefixLen && builtins.substring 0 prometheusAliasPrefixLen alias == prometheusAliasPrefix
       then builtins.substring prometheusAliasPrefixLen (aliasLen - prometheusAliasPrefixLen) alias
       else alias;
  prometheusModels =
    if builtins.hasAttr "models" prometheusModelCatalog
    then prometheusModelCatalog.models
    else [];
  prometheusCanonicalModelIds = builtins.map (model: model.id) prometheusModels;
  prometheusAliasQualifiedEnabled =
    if builtins.hasAttr "enabledAliases" prometheusModelCatalog
    then prometheusModelCatalog.enabledAliases
    else [];
  piAgentModelAliases = builtins.map stripProviderPrefix prometheusAliasQualifiedEnabled;
  piAgentEnabledModels =
    builtins.concatLists [
      (builtins.map (model: "${piAgentGatewayProvider}/${model}") prometheusCanonicalModelIds)
      prometheusAliasQualifiedEnabled
    ];
  prometheusModelMetadata =
    builtins.listToAttrs (
      builtins.map (model:
        {
          name = model.id;
          value = {
            descriptor =
              if builtins.hasAttr "descriptor" model
              then model.descriptor
              else model.id;
            reasoning =
              if builtins.hasAttr "reasoning" model
              then model.reasoning
              else false;
            contextWindow =
              if builtins.hasAttr "contextWindow" model
              then model.contextWindow
              else 128000;
            maxTokens =
              if builtins.hasAttr "maxTokens" model
              then model.maxTokens
              else 32768;
          };
        }
      ) prometheusModels
    );
  prometheusAliasTargets =
    if builtins.hasAttr "aliasTargets" prometheusModelCatalog
    then prometheusModelCatalog.aliasTargets
    else { };
  mkPrometheusModelEntry = modelId:
    let
      aliasTarget =
        if builtins.hasAttr modelId prometheusAliasTargets
        then builtins.getAttr modelId prometheusAliasTargets
        else null;
      canonicalId = if aliasTarget == null then modelId else aliasTarget;
      info = builtins.getAttr canonicalId prometheusModelMetadata;
      label =
        if aliasTarget == null
        then info.descriptor
        else "alias for ${info.descriptor}";
    in
    {
      id = modelId;
      name = "prometheus/${modelId} (${label})";
      reasoning = info.reasoning;
      input = [ "text" ];
      contextWindow = info.contextWindow;
      maxTokens = info.maxTokens;
      cost = {
        input = 0;
        output = 0;
        cacheRead = 0;
        cacheWrite = 0;
      };
    };
  piAgentModels = {
    providers = {
      ${piAgentGatewayProvider} = {
        baseUrl = "http://127.0.0.1:11435/v1";
        api = "openai-completions";
        apiKey = piAgentGatewayApiKey;
        models = builtins.map mkPrometheusModelEntry (prometheusCanonicalModelIds ++ piAgentModelAliases);
      };
    };
  };
  piAgentSettings = {
    defaultProvider =
      if builtins.hasAttr "defaultProvider" prometheusModelCatalog
      then prometheusModelCatalog.defaultProvider
      else piAgentGatewayProvider;
    defaultModel =
      if builtins.hasAttr "defaultModel" prometheusModelCatalog
      then prometheusModelCatalog.defaultModel
      else (if builtins.length piAgentModelAliases > 0 then builtins.head piAgentModelAliases else "main-deepseek");
    enabledModels = piAgentEnabledModels;
    hideThinkingBlock = false;
    defaultThinkingLevel = "medium";
    compaction = { enabled = false; };
  };
  piAgentModelsJson = toJSON piAgentModels;
  piAgentSettingsJson = toJSON piAgentSettings;

  AIPackages = with pkgs; [
    gemini-cli
    claude-code
    codex
    opencode
    llama-cpp
    litellmProxy
  ];

  nixpkgsPackages =
    with pkgs;
    [
      mksh # saner bash
      retry
      ncpamixer
      flac
      shntool
      dvtm
      abduco # Multiplexer/session
      vis # regex Editor
      tree
      ncdu # File visualizing
      unzip
      unrar
      fuse
      cryptsetup
      # Network
      sshfs-fuse
      ifmetric
      curl
      wget
      transmission_4
      tremc
      aria2 # multi-protocol download
      rsync
      nload
      nmap
      iftop
      # Wireless
      iw
      wirelesstools
      acpi
      sox # audio capture
      tio # serial tty
      androidenv.androidPkgs.platform-tools # adb/fastboot
      #== rust
      sd
      ripgrep
      fd
      eza
      bat
      broot
      eva # tui calculator
    ]
    ++ bleedingEdgeGraphicalPackages # (Todo configure)
    ++ modernGraphicalPackages # (Todo configure)
    ++ (optionals isCodeDev unixDeveloperPackages)
    ++ (optionals isMultimediaDev (
      with pkgs;
      [
        inkscape
      ]
    ));

  worldPackages = with world; [
    skrips.user
  ];

in
assert builtins.length prometheusModels > 0;
mkIf sizedAtLeast.min {
  fonts.fontconfig = {
    enable = true;
    # TODO
    defaultFonts = {
      monospace = [ ];
      sansSerif = [ ];
      serif = [ ];
      emoji = [ ];
    };
  };

  services = {
    dunst = {
      enable = !sizedAtLeast.min;
      # (TODO theme)
      settings = {
        global = {
          geometry = "300x5-30+50";
          transparency = 10;
          frame_color = "#eceff1";
          font = "Fira Code 10";
        };

        urgency_normal = {
          background = "#37474f";
          foreground = "#eceff1";
          timeout = 10;
        };
      };
    };

    gammastep = {
      enable = true;
      provider = "geoclue2";
      temperature = {
        day = 3500;
        night = 2700;
      };
    };

    gpg-agent = {
      enable = true;
      verbose = true;
      pinentry.package = pkgs.pinentry-gnome3;
      defaultCacheTtl = 10800;
      maxCacheTtl = 86400;
      defaultCacheTtlSsh = 3600;
      maxCacheTtlSsh = 86400;
      enableSshSupport = true;
      sshKeys = (optional hasPreCriome user.preCriomes.${node.name}.keygrip);
    };

    mpd = {
      enable = true;
      musicDirectory = "~/Music";
    };

    pueue = {
      enable = isCodeDev;
      settings = {
        shared = { };
        client = {
          dark_mode = dark;
        };
        daemon = {
          default_parallel_tasks = 1;
        };
      };
    };

    swaync = {
      enable = sizedAtLeast.min;
    };
  };

  programs = {
    bat = {
      enable = true;
      config = {
        theme = "gruvbox-${if dark then "dark" else "light"}";
        pager = "less -FR";
      };
    };

    direnv = {
      enable = isCodeDev;
      nix-direnv.enable = isCodeDev;
    };

    foot = {
      enable = true;
      settings = {
        main = {
          include = toString footThemeFile;
          font = "${terminalFontFamily}:size=14";
        };
      };
    };

    fzf = {
      enable = true;
      colors = fzfColors;
      defaultCommand = "fd --type f";
      defaultOptions = [ fzfBindsString ];
    };

    git = {
      enable = true;
      userEmail = methods.emailAddress;
      userName = name;
      signing = mkIf hasPreCriome {
        key = gitSigningKey;
        signByDefault = true;
      };
      extraConfig = {
        pull.rebase = true;
        init.defaultBranch = "main";
        github.user = githubId;
        ghq.root = "/git";
        hub.protocol = "ssh";
      };
    };

    gpg = {
      enable = true;
      settings = { };
    };

    joshuto = {
      enable = isCodeDev;
    };

    jujutsu = {
      enable = isCodeDev;
      settings = {
        ui = {
          diff-instructions = false;
          diff-formatter = [
            "difft"
            "--color=always"
            "$left"
            "$right"
          ];
        };
        user = {
          name = name;
          email = methods.emailAddress;
        };
        signing = mkIf hasPreCriome {
          behavior = "own";
          backend = "gpg";
          key = gitSigningKey;
        };
      };
    };

    lapce = {
      enable = isCodeDev;
      plugins = [ ];
      settings = {
        core = {
          modal = true;
          color-theme = if dark then "Lapce Dark" else "Lapce Light";
        };
        editor = {
          font-family = "FiraCode Nerd Font";
          font-size = 16;
          bracket-pair-colorization = true;
          highlight-matching-brackets = true;
        };
        ui = {
          open-editors-visible = false;
          font-size = 14;
        };
      };
    };

    # TODO broken
    zed-editor = {
      enable = isCodeDev;
      package = pkgs.zed-editor;
      extraPackages = with pkgs; [ ];
      userKeymaps = optionalAttrs useColemak colemakZedKeys;
      userSettings =
        let
          darkTheme = "base16-bright";
          lightTheme = "base16-selenized-white";
        in
        {
          theme = if dark then darkTheme else lightTheme;
          vim_mode = true;
        };
      extensions = [ ];
    };

    bottom = {
      enable = true;
      settings = { };
    };

    starship = {
      enable = true;
    };

    zsh = {
      enable = true;
      dotDir = ".config/zsh";
      history = {
        ignoreDups = true;
        expireDuplicatesFirst = true;
      };

      defaultKeymap = "viins";

      sessionVariables = {
        RSYNC_OLD_ARGS = 1;
      };

      shellAliases = {
        tsync = "rsync --progress --recursive";
        nsync = "rsync --checksum --progress --recursive";
        dsync = "rsync --checksum --progress --recursive --delete";
      };

      initContent =
        builtins.readFile ../nonNix/zshrc
        + ''
          if [[ $options[zle] = on ]]; then
          . ${pkgs.zsh-fzf-tab}/share/fzf-tab/fzf-tab.zsh
          fi
        ''
        + (optionalString useColemak (builtins.readFile ../nonNix/colemak.zsh));
    };

    zoxide.enable = true;
  };

  home = {
    packages = fontPackages ++ nixpkgsPackages ++ worldPackages ++ AIPackages;

    pointerCursor = {
      package = pkgs.vanilla-dmz;
      name = "Vanilla-DMZ";
    };

    file =
      {
        ".config/gtk-3.0/settings.ini".text = ''
          [Settings]
          gtk-application-prefer-dark-theme=${if dark then "1" else "0"}
        '';

        ".config/IJHack/QtPass.conf".text = ''
          [General]
          autoclearSeconds=20
          passwordLength=32
          useTrayIcon=false
          hideContent=false
          hidePassword=true
          clipBoardType=1
          hideOnClose=false
          passExecutable=${waylandPass}/bin/pass
          passTemplate=login\nurl
          pwgenExecutable=${pkgs.pwgen}bin/pwgen
          startMinimized=false
          templateAllFields=false
          useAutoclear=true
          useTrayIcon=false
          version=${pkgs.qtpass.version}
        '';

        ".config/broot/conf.toml".text = brootConfig;
      }
      // (optionalAttrs isOuranosNode {
        ".config/litellm-router.yaml".text = litellmRouterYaml;
        ".pi/agent/models.json".text = piAgentModelsJson;
        ".pi/agent/settings.json".text = piAgentSettingsJson;
      })
      // (optionalAttrs isPrometheusNode {
        ".config/prometheus-llama/.keep".text = "";
        ".local/share/prometheus-llama/.keep".text = "";
      });
  };

  systemd = {
    user.services =
      (optionalAttrs isPrometheusNode {
        prometheus-llama-server = {
          Unit = {
            Description = "Local llama.cpp server for Prometheus";
            Wants = [ "network-online.target" ];
            After = [ "network-online.target" ];
          };
          Service = {
            ExecStartPre = [ "${prometheusLlamaGeneratePreset}" ];
            ExecStart = ''
              ${pkgs.llama-cpp}/bin/llama-server \
                --models-preset ${prometheusLlamaPreset} \
                --host 0.0.0.0 \
                --port ${toString prometheusLlamaPort} \
                --api-key ${prometheusLlamaApiKey} \
                --jinja \
                --reasoning-format deepseek \
                --sleep-idle-seconds 600 \
                --models-max 4 \
                --no-webui
            '';
            Restart = "on-failure";
            RestartSec = 5;
            PrivateTmp = true;
            WorkingDirectory = homeDir;
            StandardOutput = "journal";
            StandardError = "journal";
          };
          Install = {
            WantedBy = [ "default.target" ];
          };
        };
      })
      // (optionalAttrs isOuranosNode {
        litellm-gateway = {
          Unit = {
            Description = "Ouranos LiteLLM gateway";
            Wants = [ "network-online.target" ];
            After = [ "network-online.target" ];
          };
          Service = {
            ExecStart = ''
              ${litellmProxy}/bin/litellm --config ${homeDir}/.config/litellm-router.yaml --host 127.0.0.1 --port 11435
            '';
            Restart = "on-failure";
            RestartSec = 5;
            PrivateTmp = true;
          };
          Install = {
            WantedBy = [ "default.target" ];
          };
        };
      });
  };

  xdg = {
    configFile = {
      "fontconfig/conf.d/10-CriomOS-fonts-paths.conf".text = mkFontConf;
    };

    mimeApps = {
      enable = true;
      defaultApplications =
        let
          defaultBrowser = "chromium.desktop";
          defaultMailer = "evolution.desktop";
          defaultAudioPlayer = "mpv.desktop";
        in
        {
          "audio/x-m4b" = defaultAudioPlayer;
          "application/zip" = "org.gnome.FileRoller.desktop";

          "x-scheme-handler/tg" = "org.telegram.desktop.desktop";
          "x-scheme-handler/tonsite" = "org.telegram.desktop.desktop";

          "application/epub+zip" = "calibre-ebook-viewer.desktop";
          "application/pdf" = "org.pwmt.zathura-pdf-mupdf.desktop";

          "text/html" = defaultBrowser;
          "x-scheme-handler/http" = defaultBrowser;
          "x-scheme-handler/https" = defaultBrowser;
          "x-scheme-handler/ftp" = defaultBrowser;
          "x-scheme-handler/chrome" = defaultBrowser;
          "application/x-extension-htm" = defaultBrowser;
          "application/x-extension-html" = defaultBrowser;
          "application/x-extension-shtml" = defaultBrowser;
          "application/xhtml+xml" = defaultBrowser;
          "application/x-extension-xhtml" = defaultBrowser;
          "application/x-extension-xht" = defaultBrowser;

          "x-scheme-handler/about" = defaultBrowser;
          "x-scheme-handler/unknown" = defaultBrowser;

          "x-scheme-handler/mailto" = defaultMailer;
          "x-scheme-handler/news" = defaultMailer;
          "x-scheme-handler/snews" = defaultMailer;
          "x-scheme-handler/nntp" = defaultMailer;
          "x-scheme-handler/feed" = defaultMailer;
          "message/rfc822" = defaultMailer;
          "application/rss+xml" = defaultMailer;
          "application/x-extension-rss" = defaultMailer;
        };
    };

  };
}
