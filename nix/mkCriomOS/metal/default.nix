{
  lib,
  horizon,
  config,
  pkgs,
  ...
}:
let
  inherit (builtins) readFile genList concatStringsSep;
  inherit (lib)
    mkIf
    optional
    optionals
    optionalString
    optionalAttrs
    isOdd
    ;
  inherit (lib.generators) toINI;
  inherit (horizon.node) typeIs;
  inherit (horizon.node.machine) model cores;
  inherit (horizon.node.methods)
    behavesAs
    sizedAtLeast
    chipIsIntel
    modelIsThinkpad
    useColemak
    computerIs
    ;

  enableWaydroid = sizedAtLeast.max && behavesAs.edge;

  # TODO
  hasTouchpad = true;

  needsIntelThrottlingFix = model == "ThinkPadT14Intel";

  hasQuickSyncSupport = builtins.elem model [
    "ThinkPadE15Gen2Intel"
    "ThinkPadT14Intel"
  ];

  modelFirmwareIndex = with pkgs; {
    ThinkPadE15Gen2Intel = [ sof-firmware ];
    ThinkPadT14Intel = [ sof-firmware ];
    rpi3B = [ raspberrypiWirelessFirmware ];
  };

  modelSpecificFirmware = modelFirmwareIndex."${model}" or [ ];

  izX230 = model == "ThinkPadX230";
  izX240 = model == "ThinkPadX240";

  hasModelSpecificPowerTweaks = model == "ThinkPadE15Gen2Intel";

  modelSpecificPowerTweaks = {
    ThinkPadE15Gen2Intel = {
      powerUpCommands = ''
        echo 0 > /sys/devices/platform/thinkpad_acpi/leds/tpacpi::power/brightness
      '';
      powerDownCommands = "";
    };
  };

  soundCardIndex = {
    ThinkPadX230 = "PCH";
    ThinkPadX240 = "PCH";
  };

  mainSoundCard = soundCardIndex."${model}" or "0";

  modelKernelModulesIndex = {
    ThinkPadE15Gen2Intel = [
      "nvme"
      "thunderbolt"
    ];
    ThinkPadT14Intel = [
      "nvme"
      "thunderbolt"
      "sd_mod"
    ];
    ThinkPadT14Gen5Intel = [
      "nvme"
      "thunderbolt"
      "sd_mod"
      "xe"
    ];
    ThinkPadX250 = [
      "usb_storage"
      "rtsx_pci_sdmmc"
    ];
  };

  modelSpecificKernelModules = modelKernelModulesIndex."${model}" or [ ];

  # (Todo Hack)
  useVaapiIntel = true;
  hasOpenClSupport = sizedAtLeast.max;

  waydroidPackages = with pkgs; [
    # Investigate: Clipboard passing still not working reliably
    wl-clipboard
    python3Packages.pyclip
  ];

  hasRecentIntelGpu = builtins.elem model [ "ThinkPadT14Gen5Intel" ];

  intelGraphicsPackages =
    optional useVaapiIntel pkgs.vaapiIntel
    ++ optional hasRecentIntelGpu pkgs.vpl-gpu-rt
    ++ optional hasOpenClSupport pkgs.intel-compute-runtime;

  # TODO - sort out different `sizedatleast` sets
  printingDriversPkgs = lib.optionals sizedAtLeast.max (
    with pkgs;
    [
      gutenprint # Drivers for many different printers from many different vendors.
      gutenprintBin # Additional, binary-only drivers for some printers.
      hplip # Drivers for HP printers.
      hplipWithPlugin # Drivers for HP printers, with the proprietary plugin.
      postscript-lexmark # Postscript drivers for Lexmark
      samsung-unified-linux-driver # Proprietary Samsung Drivers
      splix # Drivers for printers supporting SPL (Samsung Printer Language).
      brlaser # Drivers for some Brother printers
      brgenml1lpr # Generic drivers for more Brother printers
      brgenml1cupswrapper # ^^^
      cnijfilter2 # Drivers for some Canon Pixma devices (Proprietary driver)
      epson-escpr2 # Drivers for Epson AirPrint devices
      epson-escpr # Drivers for some other Epson devices
    ]
  );

  intelUtils = with pkgs; [
    libva-utils
    i7z
  ];

in
{
  hardware = {
    cpu.intel.updateMicrocode = chipIsIntel;

    # Hack: TODO - tune per model, see `modelSpecificfirmware`
    enableAllFirmware = true;

    firmware = modelSpecificFirmware;

    ledger.enable = typeIs.edge;

    graphics.extraPackages =
      optionals chipIsIntel intelGraphicsPackages
      ++ optional hasQuickSyncSupport pkgs.intel-media-driver;

  };

  location.provider = if sizedAtLeast.min then "geoclue2" else "manual";

  boot = {
    extraModulePackages =
      [ ]
      ++ (optional modelIsThinkpad config.boot.kernelPackages.acpi_call)
      ++ (optional sizedAtLeast.max config.boot.kernelPackages.v4l2loopback);

    initrd = {
      availableKernelModules = [
        "xhci_pci"
        "usb_storage"
      ];
    };

    kernelModules = [ "coretemp" ] ++ modelSpecificKernelModules;

    extraModprobeConfig = (
      optionalString sizedAtLeast.max ''
        options v4l2loopback devices=2 card_label="camera","obs" exclusive_caps=1
      ''
    );

    kernelParams = optionals computerIs.rpi3B [
      "cma=32M"
      "console=ttyS0,115200n8"
      "console=ttyAMA0,11520n8"
      "console=tty0"
      "dtparam=audio=on"
    ];

  };

  powerManagement =
    {
      powertop.enable = true;
    }
    // (optionalAttrs hasModelSpecificPowerTweaks modelSpecificPowerTweaks."${model}")
    // (optionalAttrs chipIsIntel {
      powerUpCommands = ''
        echo 1 > /sys/devices/system/cpu/intel_pstate/no_turbo
      '';
    });

  programs = { };

  console.useXkbConfig = useColemak;

  environment = {
    systemPackages =
      with pkgs;
      [ lm_sensors ]
      ++ optionals chipIsIntel intelUtils
      ++ optionals sizedAtLeast.max [ v4l-utils ]
      ++ optionals enableWaydroid waydroidPackages;

  };

  users.groups.plugdev = { };

  services = {
    # TODO
    fwupd.enable = true;

    geoclue2 = {
      enable = sizedAtLeast.min;
      enableDemoAgent = lib.mkOverride 0 true;
      geoProviderUrl = "https://beacondb.net/v1/geolocate";
      appConfig.redshift = {
        isAllowed = true;
        isSystem = true;
      };
    };

    localtimed = {
      enable = sizedAtLeast.min;
    };

    printing = {
      enable = true;
      cups-pdf.enable = sizedAtLeast.min;
      drivers = printingDriversPkgs;
    };

    throttled.enable = needsIntelThrottlingFix;

    udev.extraRules = ''
      # USBasp - USB programmer for Atmel AVR controllers
      SUBSYSTEM=="usb", ATTRS{idVendor}=="16c0", ATTRS{idProduct}=="05dc", GROUP="plugdev"
      # Pro-micro kp-boot-bootloader - Ergodone keyboard
      SUBSYSTEM=="usb", ATTRS{idVendor}=="1209", ATTRS{idProduct}=="bb05", GROUP="plugdev"
      # TODO - Broken
      # SUBSYSTEM!="usb", GOTO="librem5_rules_end"
      # Librem 5 USB flash
      # ATTR{idVendor}=="1fc9", ATTR{idProduct}=="012b", GROUP+="plugdev", TAG+="uaccess"
      # ATTR{idVendor}=="0525", ATTR{idProduct}=="a4a5", GROUP+="plugdev", TAG+="uaccess"
      # ATTR{idVendor}=="0525", ATTR{idProduct}=="b4a4", GROUP+="plugdev", TAG+="uaccess"
      # ATTR{idVendor}=="316d", ATTR{idProduct}=="4c05", GROUP+="plugdev", TAG+="uaccess"
      # LABEL="librem5_rules_end"
    '';

    libinput = {
      enable = hasTouchpad;
      touchpad = {
        naturalScrolling = true;
        tapping = true;
      };
    };

    xserver = {
      xkb.variant = optionalString useColemak "colemak";
      xkb.options = "caps:ctrl_modifier, altwin:swap_alt_win";

      autoRepeatDelay = 200;
      autoRepeatInterval = 28;

      digimend.enable = false; # !typeIs.center; # Broken
    };

    logind = {
      lidSwitch = if typeIs.center then "ignore" else "suspend";
      lidSwitchExternalPower = if behavesAs.lowPower then "suspend" else "ignore";
    };

    thinkfan = mkIf modelIsThinkpad {
      enable = true;
      # TODO
      sensors = [
        {
          type = "hwmon";
          query = "/sys/devices/virtual/thermal/thermal_zone0/temp";
        }
      ];
    };

    udisks2.enable = true;

    acpid = {
      enable = true;

      handlers = {
        mute = {
          action = ''
            wpctl set-mute @DEFAULT_AUDIO_SINK@ toggle
          '';
          event = "button/mute";
        };
        volumeup = {
          action = ''
            wpctl set-volume @DEFAULT_AUDIO_SINK@ 5%+
          '';
          event = "button/volumeup";
        };
        volumedown = {
          action = ''
            wpctl set-volume @DEFAULT_AUDIO_SINK@ 5%-
          '';
          event = "button/volumedown";
        };
        mutemic = {
          action = ''
            wpctl set-mute @DEFAULT_AUDIO_SOURCE@ toggle
          '';
          event = "button/f20";
        };
        # TODO - increase interval rise
        brightnessup = {
          action = ''
            ${pkgs.light}/bin/light -A 1
          '';
          event = "video/brightnessup";
        };
        brightnessdown = {
          action = ''
            ${pkgs.light}/bin/light -U 1
          '';
          event = "video/brightnessdown";
        };
      };

    };
  };

  virtualisation = {
    libvirtd.enable = (sizedAtLeast.max && behavesAs.edge);
    waydroid.enable = enableWaydroid;
    spiceUSBRedirection.enable = sizedAtLeast.max;
  };
}
